import java.security.MessageDigest
import org.gradle.internal.os.OperatingSystem

import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'maven-publish'

configurations {
    archives
}

publishing {
    repositories {
        maven {
            url "${System.getProperty('user.home')}/releases/maven/development"
        }
        maven {
            url "${System.getProperty('user.home')}/releases/maven/release"
        }
    }
}

def getPlatform() {
    def platform
    def os_name = System.getProperty("os.name")
    def os_arch = System.getProperty("os.arch")

    if (os_arch == 'amd64') {
        os_arch = 'x86_64'
    } else if (os_arch == 'i386') {
        os_arch = 'x86'
    } else if (os_arch == 'aarch64' || os_arch == 'arm64') {
        os_arch = 'arm64'
    }

    if (OperatingSystem.current().isWindows()) {
        platform = "windows-${os_arch}"
    } else if (OperatingSystem.current().isLinux()) {
        platform = "linux-${os_arch}"
    } else if (OperatingSystem.current().isMacOsX()) {
        platform = "osx-${os_arch}"
    } else {
        platform = "${os_name}-${os_arch}"
    }
    return platform
}

def getTriplet() {
    def platform
    def os_name = System.getProperty("os.name")
    def os_arch = System.getProperty("os.arch")
    def triplet_arch = ""
    def triplet_name = ""

    if (os_arch == 'amd64') {
        triplet_arch = "x64"
    } else if (os_arch == 'i386') {
        triplet_arch = "x86"
    } else if (os_arch == 'aarch64' || os_arch == 'arm64') {
        triplet_arch = "arm64"
    } else {
        triplet_arch = "x64"
    }

    if (OperatingSystem.current().isWindows()) {
        triplet_name = "windows"
    } else if (OperatingSystem.current().isLinux()) {
        triplet_name = "linux"
    } else if (OperatingSystem.current().isMacOsX()) {
        triplet_name = "osx"
    }
    return triplet_arch + "-" + triplet_name
}

def getPlatformPath(platform) {
    if (platform == "linux-athena") {
        return "linux/athena"
    } else if (platform == "linux-arm32") {
        return "linux/arm32"
    } else if (platform == "linux-arm64") {
        return "linux/arm64"
    } else if (platform == "linux-x86") {
        return "linux/x86"
    } else if (platform == "linux-x86_64") {
        return "linux/x86-64"
    } else if (platform == "osx-x86") {
        return "osx/x86"
    } else if (platform == "osx-x86_64") {
        return "osx/x86-64"
    } else if (platform == "osx-arm64") {
        return "osx/arm64"
    } else if (platform == "windows-x86") {
        return "windows/x86"
    } else if (platform == "windows-x86_64") {
        return "windows/x86-64"
    } else if (platform == "windows-arm64") {
        return "windows/arm64"
    } else {
        return ""
    }
}

ext.platform = getPlatform()
ext.platformPath = getPlatformPath(ext.platform)
ext.platformClassifier = project.platform.replaceFirst('-', '').replace('_', '-')

ext.repo = "thirdparty-ceres"

def triplet = getTriplet()

def pubVersion = "2.2-1"

def outputsFolder = file("$project.buildDir/outputs")

def baseArtifactId = 'ceres'
def artifactGroupId = 'edu.wpi.first.thirdparty.frc2024.ceres'
def zipBaseName = '_GROUP_edu_wpi_first_thirdparty_frc2024_ceres_ID_ceres-cpp_CLS'

def versionFile = file("$outputsFolder/version.txt")
def licenseFile = file("LICENSE.md")

def outputClassifierStatic = project.ext.platformClassifier + 'static'
def outputClassifierDebugStatic = project.ext.platformClassifier + 'debug/static'
System.out.println(triplet)


task copyAllOutputs(type: Copy) {
    destinationDir = outputsFolder
}

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archiveFile
    copyAllOutputs.from task.archiveFile
}

task outputVersions() {
    description = 'Prints the versions of this to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

task cppSourcesZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = zipBaseName
    archiveClassifier = "sources"

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve("buildtrees")
    from(resolvedDir.toFile()) {
        into '/source'
        include '**/*.cpp', '**/*.cc'
    }

    includeEmptyDirs = false
}

task cppHeadersZip(type: Zip) {
    dependsOn outputVersions
    destinationDirectory = outputsFolder
    archiveBaseName = zipBaseName
    archiveClassifier = "headers"

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("include")
    from(resolvedDir.toFile()) {
        into '/include'
        include '**/*.hpp', '**/*.h'
    }

    includeEmptyDirs = false
}

task cppLibsZipStatic(type: Zip) {
    destinationDirectory = outputsFolder
    archiveClassifier = outputClassifierStatic
    archiveBaseName = zipBaseName
    duplicatesStrategy = 'exclude'

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("lib")
    from(resolvedDir.toFile()) {
        into project.platformPath + '/static'
        include '**/*.a'
        include '**/*.lib'
        include '**/*.pdb'
    }
}

task cppLibsZipStaticDebug(type: Zip) {
    destinationDirectory = outputsFolder
    archiveClassifier = outputClassifierStatic + 'debug'
    archiveBaseName = zipBaseName + 'debug'
    duplicatesStrategy = 'exclude'

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("debug").resolve("lib")
    from(resolvedDir.toFile()) {
        into project.platformPath + '/debug/static'
        include '**/*.a'
        include '**/*.lib'
        include '**/*.pdb'
    }
}

if (!project.hasProperty('skipRelease')) {
    addTaskToCopyAllOutputs(cppLibsZipStatic)
}

if (!project.hasProperty('skipDebug')) {
    addTaskToCopyAllOutputs(cppLibsZipStaticDebug)
}

if (!project.hasProperty('skipSources')) {
    addTaskToCopyAllOutputs(cppSourcesZip)
    addTaskToCopyAllOutputs(cppHeadersZip)
}

if (project.hasProperty('runmerge')) {
    apply from: 'merge.gradle'
}



model {
    publishing {
        publications {
            cpp(MavenPublication) {
                artifact cppHeadersZip
                artifact cppLibsZipStatic
                artifact cppLibsZipStaticDebug

                artifactId = "${baseArtifactId}-cpp"
                groupId artifactGroupId
                version pubVersion
            }
        }
        repositories {
            maven {
                url "${System.getProperty('user.home')}/releases/maven/${project.repo}"
            }
        }
    }
}